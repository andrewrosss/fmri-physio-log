start: body footer

body: params data
params: detailed_params | simple_params
detailed_params: INT INT INT INT [INT] info
simple_params: INT INT INT INT
info: "5002" INFO_BODY "6002"
data: (info | "5000" | INT)+

footer: "5003" footer_line+ "6003"
footer_line: rate_line | stat_line | nr_line | log_line

rate_line: modality rate
rate: rate_prefix INT INT
rate_prefix: "Freq Per:"

stat_line: modality stat
stat: stat_prefix INT INT INT INT
stat_prefix: "Min Max Avg StdDiff:"

nr_line: nr_prefix INT INT INT INT
nr_prefix: "NrTrig NrMP NrArr AcqWin:"

log_line: log_prefix INT
log_prefix: "LogStartMDHTime:"
          | "LogStopMDHTime:"
          | "LogStartMPCUTime:"
          | "LogStopMPCUTime:"

modality: "ECG" | "PULS" | "RESP" | "EXT" | "EXT2"

INFO_BODY: /[^\s]+.+?(?=\s*?6002)/

%import common.INT
%import common.WS
%ignore WS
