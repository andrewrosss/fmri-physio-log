start: data footer

data: (info | "5000" | "6000" | INT)+ // 5000 and 6000 will be ignored

info: "5002" INFO_BODY "6002"
INFO_BODY: /[^\s]+.+?(?=\s+?6002\s+?)/

footer: "5003" footer_line+ "6003"
footer_line: rate_line | stat_line | nr_line | log_line

rate_line: modality rate
rate: "Freq Per" ":" INT INT

stat_line: modality stat
stat: "Min Max Avg StdDiff" ":" INT INT INT INT

nr_line: "NrTrig NrMP NrArr AcqWin" ":" INT INT INT INT

log_line: _log_type ":" INT
_log_type: LOG_START_MDH_TIME
         | LOG_STOP_MDH_TIME
         | LOG_START_MPCU_TIME
         | LOG_STOP_MPCU_TIME

LOG_START_MDH_TIME: "LogStartMDHTime"
LOG_STOP_MDH_TIME: "LogStopMDHTime"
LOG_START_MPCU_TIME: "LogStartMPCUTime"
LOG_STOP_MPCU_TIME: "LogStopMPCUTime"

modality: ECG | PULS | RESP | EXT | EXT2

ECG: "ECG"
PULS: "PULS"
RESP: "RESP"
EXT: "EXT"
EXT2: "EXT2"

%import common.INT
%import common.WS
%ignore WS
